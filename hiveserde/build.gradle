

buildscript {
    repositories  {
        maven {
        url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopcryptoledger-hiveserde"
version = "1.3.2"

repositories {
  mavenCentral()
  mavenLocal()
}





jar {
    manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop CryptoLedger Analytics Library - Serde', 'Implementation-Version': version
    }

    baseName = 'hadoopcryptoledger-hiveserde'
}




jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}



test {
    systemProperty "java.awt.headless", "true"
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

configurations {
	provided
	integrationTestCompile.extendsFrom testCompile
    	integrationTestRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
    integrationTest {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
   // hadoop lib for driver
     compileOnly("org.apache.hadoop:hadoop-client:3.3.0")
       // hadoop crypto ledger library
   implementation("com.github.zuinnote:hadoopcryptoledger-fileformat:1.3.2")
       // bouncycastle libraries - only for Ethereum utility functions needed
    compileOnly("org.bouncycastle:bcprov-ext-jdk15on:1.70")
    // hive serde API
   compileOnly("org.apache.hive:hive-serde:3.1.2")
   compileOnly("org.apache.hive:hive-exec:3.1.2")
   // log4j2
   compileOnly("org.apache.logging.log4j:log4j-api:2.17.0")
   // test
    testImplementation("org.apache.hive:hive-serde:3.1.2")
    testImplementation("org.apache.hive:hive-exec:3.1.2")
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.zuinnote'
            artifactId 'hadoopcryptoledger-hiveserde'
            version '1.3.2'

            from components.java
        }
    }
}

javadoc.classpath += configurations.provided

task hadoopCryptoLedgerJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives hadoopCryptoLedgerJavaDocs, sourcesJar
}

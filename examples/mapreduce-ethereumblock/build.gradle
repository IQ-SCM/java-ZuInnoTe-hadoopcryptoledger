buildscript {
    repositories  {
        maven {
        url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'application'


compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = '1.0'


mainClassName = "org.zuinnote.hadoop.ethereum.example.driver.EthereumBlockCounterDriver"

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


configurations {
	provided
	testProvided
	testIntegrationCompile.extendsFrom testCompile
	testIntegrationRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
    plusConfigurations += [ configurations.testProvided ]
    plusConfigurations += [ configurations.testIntegrationCompile ]
	plusConfigurations += [ configurations.testIntegrationRuntime ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
     testIntegration {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
	}
}

dependencies {
   // hadoop lib for driver
     compileOnly("org.apache.hadoop:hadoop-client:3.3.0")
       // hadoop crypto ledger library
   implementation("com.github.zuinnote:hadoopcryptoledger-fileformat:1.3.2")
   // testing

       // for  testing we can only use 2.7.x, because higher versions of Hadoop have a bug in minidfs-cluster. Nevertheless, the library itself works also with higher Hadoop versions 
    // see https://issues.apache.org/jira/browse/HDFS-5328
    testImplementation("org.apache.hadoop:hadoop-common:2.7.0")
     testImplementation("org.apache.hadoop:hadoop-client:2.7.0")
    testImplementation group: 'org.jmockit', name: 'jmockit', version: '1.48'
     testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
     testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
       testIntegrationImplementation("com.github.zuinnote:hadoopcryptoledger-fileformat:1.3.2")
     testIntegrationImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
    testIntegrationRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
	testIntegrationRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-console', version: '1.8.2'
    // for integration testing we can only use 2.7.x, because higher versions of Hadoop have a bug in minidfs-cluster. Nevertheless, the library itself works also with higher Hadoop versions 
    // see https://issues.apache.org/jira/browse/HDFS-5328
    testIntegrationImplementation("org.apache.hadoop:hadoop-common:2.7.0")
     testIntegrationImplementation("org.apache.hadoop:hadoop-client:2.7.0")
     testIntegrationImplementation group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: '2.7.0'
}


shadowJar {
      manifest {
        attributes 'Implementation-Title': 'Example - MR job (EthereumBlock) for analysing Ethereum data using hadoopcryptoledger', 'Implementation-Version': version
        }
    baseName = 'example-hcl-mr-ethereumblock'
    version = '0.1.0'
   exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' // remove signature digests of other libraries
 }



task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    useJUnitPlatform()
}

check.dependsOn testIntegration
testIntegration.mustRunAfter test

